Trendline, Genesis Percentage of Patients by Quarter = 
// Credit: gist.github.com/deldersveld/62523ca8350ac97797131560cb317677
// Static line color
VAR LineColor = "#01B8AA"
// "Date" field used in this example along the X axis
VAR XMinDate = MIN(All_Cases[dateindex])
VAR XMaxDate = MAX(All_Cases[dateindex])
// Obtain overall min and overall max measure values when evaluated for each date
VAR YMinValue = MINX(VALUES(All_Cases[dateindex]),CALCULATE([G_% Patients_AQ]))
VAR YMaxValue = MAXX(VALUES(All_Cases[dateindex]),CALCULATE([G_% Patients_AQ]))
// Build table of X & Y coordinates and fit to 100 x 100 viewbox
VAR SparklineTable = ADDCOLUMNS(
    SUMMARIZE(All_Cases,All_Cases[dateindex]),
        "X",INT(200 * DIVIDE(All_Cases[dateindex] - XMinDate, XMaxDate - XMinDate)),
        "Y",INT(100 * DIVIDE([G_% Patients_AQ] - YMinValue,YMaxValue - YMinValue)))
// Concatenate X & Y coordinates to build the sparkline
VAR Lines = CONCATENATEX(SparklineTable,[X] & "," & 100-[Y]," ", [dateindex])
// Add to SVG, and verify Data Category is set to Image URL for this measure
VAR SVGImageURL =
    "data:image/svg+xml;utf8," & 
    "<svg xmlns='http://www.w3.org/2000/svg' x='0px' y='0px' viewBox='0 0 200 100'>" &
     "<polyline fill='none' stroke='" & LineColor & 
     "' stroke-width='3' points='" & Lines & 
     "'/></svg>"
RETURN SVGImageURL
